plugins {
	id 'java'
	id 'idea'
	//code quality control
	id 'pmd'
	id 'jacoco'
	id "name.remal.sonarlint" version "4.2.6"
	id "com.diffplug.spotless" version "6.25.0"
}

group = 'com.github.ducknowledges.calories_tracker'
version = '0.0.1-SNAPSHOT'
compileJava.options.encoding = 'UTF-8'

wrapper {
	gradleVersion '8.9'
}

// Plugin Config
java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}


idea {
	project {
		languageLevel = 17
	}
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

pmd {
	group = 'verification'
	consoleOutput = true
	toolVersion = "7.4.0"
	ruleSetFiles = files('pmd-rules.xml')
}

spotless {
	java {
		removeUnusedImports()
		googleJavaFormat('1.23.0')
		formatAnnotations()
	}
}


ext {
	versions = [
			'junit': '5.11.0'
	]
}

dependencies {
	//Test Dependencies
	testImplementation 'org.assertj:assertj-core:3.26.3'
	testImplementation "org.junit.jupiter:junit-jupiter:${versions.junit}"
}

configurations.configureEach {
	resolutionStrategy.failOnVersionConflict()
	resolutionStrategy.force 'org.sonarsource.analyzer-commons:sonar-analyzer-commons:2.11.0.2861'
	resolutionStrategy.force 'org.sonarsource.analyzer-commons:sonar-xml-parsing:2.11.0.2861'
	resolutionStrategy.force 'org.sonarsource.sslr:sslr-core:1.24.0.633'
	resolutionStrategy.force 'org.sonarsource.analyzer-commons:sonar-analyzer-recognizers:2.11.0.2861'
	resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
	resolutionStrategy.force 'org.apache.commons:commons-lang3:3.14.0'
	resolutionStrategy.force 'commons-io:commons-io:2.15.1'
}

//Print dependencies from BOMs: Task -> other -> managedVersions
tasks.register('managedVersions') {
	doLast {
		dependencyManagement.managedVersions.each {
			print it.key + ':' + it.value + '\n'
		}
	}
}

//Build Project
compileJava {
	options.compilerArgs << '-Werror'
	dependsOn spotlessApply
}

jar {
	archiveFileName.set("${project.name}-${project.version}.jar")
	dependsOn('codeQuality')
}

// Code Quality Control
tasks.register("codeQuality") {
	description = 'Runs code quality control.'
	group = 'verification'
	dependsOn sonarlintMain
	dependsOn sonarlintTest
	dependsOn pmdMain
	dependsOn pmdTest
	dependsOn test
	dependsOn jacocoTestCoverageVerification
}


test {
	description = 'Runs unit tests.'
    useJUnitPlatform {
        filter.includeTestsMatching('*Test')
    }
	failFast = true
	maxHeapSize = '512m'
	finalizedBy jacocoTestReport
}

check {
	dependsOn codeQuality
}

jacocoTestReport {
	reports {
		xml.required = true
		html.outputLocation = layout.buildDirectory.dir('jacoco')
	}
	dependsOn test
}

jacocoTestCoverageVerification {
	violationRules {
		failOnViolation = true
		rule {
			limit {
				minimum = 0.80
			}
		}
		rule {
			limit {
				element = 'METHOD'
				counter = 'COMPLEXITY'
				value = 'TOTALCOUNT'
				maximum = 8
			}
		}
	}
	dependsOn jacocoTestReport
}



repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://jitpack.io' }
}